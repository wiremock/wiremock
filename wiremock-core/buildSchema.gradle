import tools.jackson.databind.JsonNode
import tools.jackson.databind.ObjectMapper
import tools.jackson.databind.json.JsonMapper
import tools.jackson.databind.node.ArrayNode
import tools.jackson.databind.node.ObjectNode
import tools.jackson.dataformat.yaml.YAMLMapper

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "tools.jackson.core:jackson-core:3.0.3"
    classpath "tools.jackson.core:jackson-databind:3.0.3"
    classpath "tools.jackson.dataformat:jackson-dataformat-yaml:3.0.3"
  }
}

def buildStubMappingSchema = tasks.register("buildStubMappingSchema") {
  group = "build"
  outputs.dir(temporaryDir)
  inputs.dir('src/main/resources/swagger/schemas')

  doLast {
    def schemasDir = new File(temporaryDir, "schemas")
    schemasDir.mkdir()
    new File(schemasDir, "wiremock-stub-mapping.json").write(doBuildStubMappingSchema())
  }
}

def buildMessageStubMappingSchema = tasks.register("buildMessageStubMappingSchema") {
  group = "build"
  outputs.dir(temporaryDir)
  inputs.dir('src/main/resources/swagger/schemas')

  doLast {
    def schemasDir = new File(temporaryDir, "schemas")
    schemasDir.mkdir()
    new File(schemasDir, "wiremock-message-stub-mapping.json").write(doBuildMessageStubMappingSchema())
  }
}

sourceSets {
  main {
    resources {
      srcDir(buildStubMappingSchema.map { it.temporaryDir })
      srcDir(buildMessageStubMappingSchema.map { it.temporaryDir })
    }
  }
}

/**
 * Builds a single-file JSON schema by merging the files in {@code src/main/resources/com/intellij/wiremock/schemas}.
 *
 * <pre>
 * {
 *   "title": "WireMock stub mapping",
 *   "type": "object",
 *   "$schema": "http://json-schema.org/draft-04/schema#",
 *   "definitions" : {
 *     "absent-pattern" : {
 *       "title" : "Absent matcher",
 *       "type" : "object",
 *       "properties" : {
 *         "absent" : {
 *           "type" : "boolean"
 *         }
 *       },
 *       "required" : [ "absent" ]
 *     },
 *     ...
 *   },
 *   "oneOf" : [ {
 *     "$ref" : "#/definitions/stub-mapping"
 *   }, {
 *     "$ref" : "#/definitions/stub-mappings"
 *   } ]
 * }
 * </pre>
 */
private String doBuildStubMappingSchema() {
  def jsonMapper = JsonMapper.builder().build();
  def document = jsonMapper.createObjectNode()
  document.put("title", "WireMock stub mapping")
  document.put("type", "object")

  // Only draft-07 is supported by schema-store.org so we bump up to this despite strictly being on 04 within our OpenAPI version
  document.put("\$schema", "http://json-schema.org/draft-07/schema#")

  def yamlMapper = YAMLMapper.builder().build()

  def schemas = new ArrayList<Tuple>()
  file('src/main/resources/swagger/schemas').listFiles().toList().sort { it.name }.each { file ->
    def name = file.name - ".yaml"
    def schema = readSchema(yamlMapper, file)

    //For non-'date-time-elements', adds a single name-schema pair,
    // while for date-time-elements file adds a separate name-schema for each schema in that yaml file
    if (name != "date-time-elements") {
      schemas.add(Tuple.tuple(name, schema))
    } else {
      ((ObjectNode) schema).properties().each { prop ->
        schemas.add(Tuple.tuple(prop.getKey(), prop.getValue()))
      }
    }
  }

  //The content of each referenced schema file is extracted and added under a common property called 'definitions'.
  def definitions = document.putObject("definitions")
  schemas.each { keyAndNode -> definitions.putIfAbsent(keyAndNode[0], keyAndNode[1]) }

  def oneOf = document.putArray("oneOf")
  oneOf.addObject().put("\$ref", "#/definitions/stub-mapping")
  oneOf.addObject().put("\$ref", "#/definitions/stub-mappings")

  return jsonMapper
      .writerWithDefaultPrettyPrinter()
      .writeValueAsString(document)
}

private JsonNode readSchema(ObjectMapper objectMapper, File file) {
  def node = objectMapper.readTree(file)
  replaceFileRefs(node)
  return node
}

/**
 * Replaces refs as follows:
 * <ul>
 *   <li>{@code "$ref": "content-pattern.yaml"} -> {@code "$ref": "#/definitions/content-pattern"}</li>
 *   <li>{@code "$ref": "date-time-elements.yaml#/format"} -> {@code "$ref": "#/definitions/format"}</li>
 * </ul>
 */
private def replaceFileRefs(JsonNode node) {
  if (node instanceof ObjectNode) {
    def nodeAsObject = (ObjectNode) node
    final def ref = nodeAsObject["\$ref"]?.asText()
    if (ref != null) {
      if (ref.endsWith(".yaml")) {
        nodeAsObject.put("\$ref", "#/definitions/${(ref - ".yaml")}")
      }
      //date-time-elements.yaml is handled explicitly because it contains multiple standalone schemas
      else if (ref.startsWith("date-time-elements.yaml#/")) {
        nodeAsObject.put("\$ref", "#/definitions/${removePrefix(ref, "date-time-elements.yaml#/")}")
      }
    } else {
      for (child in nodeAsObject) {
        replaceFileRefs(child)
      }
    }
  } else if (node instanceof ArrayNode) {
    def nodeAsArray = (ArrayNode) node
    for (item in nodeAsArray) {
      replaceFileRefs(item)
    }
  }
}

private static String removePrefix(String input, String prefix) {
  return input.substring(prefix.length())
}

private String doBuildMessageStubMappingSchema() {
  def jsonMapper = JsonMapper.builder().build()
  def document = jsonMapper.createObjectNode()
  document.put("title", "WireMock message stub mapping")
  document.put("type", "object")

  document.put("\$schema", "http://json-schema.org/draft-07/schema#")

  def yamlMapper = YAMLMapper.builder().build()

  def schemas = new ArrayList<Tuple>()
  file('src/main/resources/swagger/schemas').listFiles().toList().sort { it.name }.each { file ->
    def name = file.name - ".yaml"
    def schema = readSchema(yamlMapper, file)

    if (name != "date-time-elements") {
      schemas.add(Tuple.tuple(name, schema))
    } else {
      ((ObjectNode) schema).properties().each { prop ->
        schemas.add(Tuple.tuple(prop.getKey(), prop.getValue()))
      }
    }
  }

  def definitions = document.putObject("definitions")
  schemas.each { keyAndNode -> definitions.putIfAbsent(keyAndNode[0], keyAndNode[1]) }

  def oneOf = document.putArray("oneOf")
  oneOf.addObject().put("\$ref", "#/definitions/message-stub-mapping")
  oneOf.addObject().put("\$ref", "#/definitions/message-stub-mappings")

  return jsonMapper
      .writerWithDefaultPrettyPrinter()
      .writeValueAsString(document)
}
