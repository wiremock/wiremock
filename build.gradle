buildscript {
    repositories {
        maven {
            url "https://oss.sonatype.org"
        }
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'scala'
    id 'signing'
    id 'maven-publish'
    id 'idea'
    id 'eclipse'
    id 'project-report'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id "org.sonarqube" version "3.3"
    id 'jacoco'
}

group = 'com.github.tomakehurst'

project.ext {
    versions = [
        handlebars     : '4.3.0',
        jetty          : '9.4.44.v20210927',
        guava          : '31.0.1-jre',
        jackson        : '2.13.0',
        xmlUnit        : '2.8.3',
        junitJupiter   : '5.8.1'
    ]
}

dependencies {
    api "org.eclipse.jetty:jetty-server:$versions.jetty"
    api "org.eclipse.jetty:jetty-servlet:$versions.jetty"
    api "org.eclipse.jetty:jetty-servlets:$versions.jetty"
    api "org.eclipse.jetty:jetty-webapp:$versions.jetty"
    api "org.eclipse.jetty:jetty-proxy:$versions.jetty"
    api "org.eclipse.jetty.http2:http2-server:$versions.jetty"
    api "org.eclipse.jetty:jetty-alpn-server:$versions.jetty"
    api "org.eclipse.jetty:jetty-alpn-java-server:$versions.jetty"
    api "org.eclipse.jetty:jetty-alpn-openjdk8-server:$versions.jetty"
    api "org.eclipse.jetty:jetty-alpn-java-client:$versions.jetty"
    api "org.eclipse.jetty:jetty-alpn-openjdk8-client:$versions.jetty"

    api "com.google.guava:guava:$versions.guava"
    api platform("com.fasterxml.jackson:jackson-bom:$versions.jackson")
    api "com.fasterxml.jackson.core:jackson-core",
        "com.fasterxml.jackson.core:jackson-annotations",
        "com.fasterxml.jackson.core:jackson-databind"
    api "org.apache.httpcomponents.client5:httpclient5:5.1"
    api "org.xmlunit:xmlunit-core:$versions.xmlUnit"
    api "org.xmlunit:xmlunit-legacy:$versions.xmlUnit", {
        exclude group: 'junit', module: 'junit'
    }
    api "org.xmlunit:xmlunit-placeholders:$versions.xmlUnit"
    api 'net.javacrumbs.json-unit:json-unit-core:2.28.0'
    api "com.jayway.jsonpath:json-path:2.6.0", {
        exclude group: 'org.ow2.asm', module: 'asm'
    }
    api "net.minidev:json-smart:2.4.7" // Pinning this above the transitive version from json-path to get CVE fix
    api "org.ow2.asm:asm:9.2"
    api "org.slf4j:slf4j-api:1.7.32"
    api "net.sf.jopt-simple:jopt-simple:5.0.4"

    compileOnly("junit:junit:4.13.2") {
        exclude group: "org.hamcrest", module: "hamcrest-core"
    }
    compileOnly(platform("org.junit:junit-bom:$versions.junitJupiter"))
    compileOnly("org.junit.jupiter:junit-jupiter")

    api 'org.apache.commons:commons-lang3:3.12.0'
    api "com.github.jknack:handlebars:$versions.handlebars", {
        exclude group: 'org.mozilla', module: 'rhino'
    }
    api("com.github.jknack:handlebars-helpers:$versions.handlebars") {
        exclude group: 'org.mozilla', module: 'rhino'
    }

    api 'commons-fileupload:commons-fileupload:1.4',  {
        exclude group: 'commons-io', module: 'commons-io'
    }
    api "commons-io:commons-io:2.11.0"

    testImplementation "junit:junit:4.13"
    testImplementation("org.junit.jupiter:junit-jupiter:$versions.junitJupiter")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine")
    testImplementation("org.junit.platform:junit-platform-launcher")
    testImplementation 'org.mockito:mockito-junit-jupiter:4.0.0'

    testImplementation "org.hamcrest:hamcrest-core:2.2"
    testImplementation "org.hamcrest:hamcrest-library:2.2"
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.0.0'
    testImplementation 'net.javacrumbs.json-unit:json-unit:2.28.0'
    testImplementation "org.skyscreamer:jsonassert:1.2.3"
    testImplementation 'com.toomuchcoding.jsonassert:jsonassert:0.6.1'
    testImplementation 'org.awaitility:awaitility:4.1.1'
    testImplementation "com.googlecode.jarjar:jarjar:1.3"
    testImplementation "commons-io:commons-io:2.10.0"
    testImplementation 'org.scala-lang:scala-library:2.13.6'
    testImplementation 'com.tngtech.archunit:archunit-junit5:0.22.0'

    testImplementation "org.eclipse.jetty:jetty-client:$versions.jetty"
    testImplementation "org.eclipse.jetty.http2:http2-http-client-transport:$versions.jetty"

    testRuntimeOnly "org.slf4j:slf4j-api:1.7.32"
    testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:2.14.1"
    testRuntimeOnly "org.apache.logging.log4j:log4j-api:2.14.1"
    testRuntimeOnly "org.apache.logging.log4j:log4j-core:2.14.1"
    testRuntimeOnly files('src/test/resources/classpath file source/classpathfiles.zip', 'src/test/resources/classpath-filesource.jar')

    testImplementation ('net.jockx:littleproxy:1.1.3') {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.apache.commons', module: 'commons-lang3'
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'io.netty', module: 'netty-all'
    }
    testImplementation "io.netty:netty-all:4.1.69.Final"
}

allprojects {
    ext {
        runningOnCI = System.getenv('CI') == 'true'

        repoUser =     this.hasProperty('sonatypeUser')     ? sonatypeUser : 'default'
        repoPassword = this.hasProperty('sonatypePassword') ? sonatypePassword : 'default'

        pomInfo = {
            name 'WireMock'
            description 'A web service test double for all occasions'
            url 'http://wiremock.org'
            scm {
                connection 'https://github.com/wiremock/wiremock.git'
                developerConnection 'https://github.com/wiremock/wiremock.git'
                url 'https://github.com/wiremock/wiremock'
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/license/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id 'tomakehurst'
                    name 'Tom Akehurst'
                }
            }
        }
    }

    repositories {
        mavenCentral()
    }

    version = '2.31.0'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava {
        options.encoding = 'UTF-8'

        // silences warnings about compiling against `sun` packages
        options.compilerArgs += '-XDenableSunApiLintControl'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
        options.compilerArgs += '-XDenableSunApiLintControl'
    }

    test {
        // Set the timezone for testing somewhere other than my machine to increase the chances of catching timezone bugs
        systemProperty 'user.timezone', 'Australia/Sydney'

        useJUnitPlatform()
        exclude 'ignored/**'

        maxParallelForks = runningOnCI ? 1 : 3

        testLogging {
            events "FAILED", "SKIPPED"
            exceptionFormat "full"
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }
    test.finalizedBy jacocoTestReport

    sonarqube {
        properties {
            property "sonar.projectKey", "wiremock_wiremock"
            property "sonar.organization", "wiremock"
            property "sonar.host.url", "https://sonarcloud.io"
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

task testJar(type: Jar, dependsOn: testClasses) {
    archiveClassifier.set('tests')
    from sourceSets.test.output
}

final DOCS_DIR = project(':').rootDir.getAbsolutePath() + '/docs-v2'
task npmInstall(type: Exec) {
    workingDir DOCS_DIR
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', 'npm', 'i'
    } else {
        commandLine 'npm', 'i'
    }
}

task generateApiDocs(type: Exec) {
    dependsOn npmInstall
    workingDir DOCS_DIR
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', 'npm', 'run', 'apidocs'
    } else {
        commandLine 'npm', 'run', 'apidocs'
    }
}

jar {
    archiveBaseName.set('wiremock-jre8')
    manifest {
        attributes("Main-Class": "com.github.tomakehurst.wiremock.standalone.WireMockServerRunner")
        attributes("Add-Exports": "java.base/sun.security.x509")
    }
}

shadowJar {
    archiveBaseName.set('wiremock-jre8-standalone')

    relocate "org.mortbay", 'wiremock.org.mortbay'
    relocate "org.eclipse", 'wiremock.org.eclipse'
    relocate "org.codehaus", 'wiremock.org.codehaus'
    relocate "com.google", 'wiremock.com.google'
    relocate "com.google.thirdparty", 'wiremock.com.google.thirdparty'
    relocate "com.fasterxml.jackson", 'wiremock.com.fasterxml.jackson'
    relocate "org.apache", 'wiremock.org.apache'
    relocate "org.xmlunit", 'wiremock.org.xmlunit'
    relocate "org.hamcrest", 'wiremock.org.hamcrest'
    relocate "org.skyscreamer", 'wiremock.org.skyscreamer'
    relocate "org.json", 'wiremock.org.json'
    relocate "net.minidev", 'wiremock.net.minidev'
    relocate "com.jayway", 'wiremock.com.jayway'
    relocate "org.objectweb", 'wiremock.org.objectweb'
    relocate "org.custommonkey", "wiremock.org.custommonkey"
    relocate "net.javacrumbs", "wiremock.net.javacrumbs"
    relocate "net.sf", "wiremock.net.sf"
    relocate "com.github.jknack", "wiremock.com.github.jknack"
    relocate "org.antlr", "wiremock.org.antlr"
    relocate "javax.servlet", "wiremock.javax.servlet"
    relocate "org.checkerframework", "wiremock.org.checkerframework"
    relocate "org.hamcrest", "wiremock.org.hamcrest"

    dependencies {
        exclude(dependency('junit:junit'))
    }

    mergeServiceFiles()

    exclude 'META-INF/maven/javax.servlet/**'
    exclude 'module-info.class'
    exclude 'handlebars-*.js'
}

javadoc {
    exclude "**/CertificateAuthority.java"
    options.addStringOption('Xdoclint:none', '-quiet')
}

signing {
    required {
        !version.toString().contains("SNAPSHOT") && (gradle.taskGraph.hasTask("uploadArchives") || gradle.taskGraph.hasTask("publish"))
    }
    sign publishing.publications
}

publishing {
    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username repoUser
                password repoPassword
            }
        }

        maven {
            name 'snapshots'
            url 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials {
                username repoUser
                password repoPassword
            }
        }
    }

    publications {
        mavenJava(MavenPublication) { publication ->
            artifactId = "${jar.getArchiveBaseName().get()}"
            from components.java
            artifact testJar

            pom.withXml {
                asNode().children().last() + pomInfo
            }
        }

        standaloneJar(MavenPublication) { publication ->
            artifactId = "${jar.getArchiveBaseName().get()}-standalone"
            project.shadow.component(publication)

            artifact sourcesJar
            artifact javadocJar
            artifact testJar

            publication.artifacts.each {
		// for the shaded JAR
                if ('all' == it.classifier) {
                    it.classifier = ''
                }
            }

            pom.packaging 'jar'
            pom.withXml {
                asNode().appendNode('description', 'A web service test double for all occasions - standalone edition')
                asNode().children().last() + pomInfo
            }
        }
    }

}

task checkReleasePreconditions {
    doLast {
        def REQUIRED_GIT_BRANCH = 'master'
        def currentGitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
        assert currentGitBranch == REQUIRED_GIT_BRANCH, "Must be on the $REQUIRED_GIT_BRANCH branch in order to release to Sonatype"
    }
}
publish.dependsOn checkReleasePreconditions

assemble.dependsOn jar, shadowJar

task addGitTag {
    doLast {
        println "git tag ${version}".execute().text
        println "git push origin --tags".execute().text
    }
}

task release {
    dependsOn clean, assemble, publishAllPublicationsToMavenRepository, addGitTag
}

task 'bump-patch-version' {
    doLast {
        def filesWithVersion = [
            'build.gradle'                                      : { "version = '${it}" },
            'docs-v2/_config.yml'                               : { "wiremock_version: ${it}" },
            'docs-v2/package.json'                              : { "version\": \"${it}\"" },
            'src/main/resources/swagger/wiremock-admin-api.yaml': { "version: ${it}" }
        ]

        def currentVersion = project.version
        def nextVersion = "${majorVersion}.${minorVersion}.${patchVersion + 1}"

        filesWithVersion.each { fileName, lineWithVersionTemplates ->
            def file = file(fileName)
            def lineWithVersionTemplateList = [lineWithVersionTemplates].flatten()

            lineWithVersionTemplateList.each { lineWithVersionTemplate ->
                def oldLine = lineWithVersionTemplate.call(currentVersion)
                def newLine = lineWithVersionTemplate.call(nextVersion)
                println "Replacing '${oldLine}' with '${newLine}' in ${fileName}"
                file.text = file.text.replace(oldLine, newLine)
            }
        }
    }
}

task 'bump-minor-version' {
    doLast {
        def filesWithVersion = [
            'build.gradle'                                      : { "version = '${it}" },
            'docs-v2/_config.yml'                               : { "wiremock_version: ${it}" },
            'docs-v2/package.json'                              : { "version\": \"${it}\"" },
            'src/main/resources/swagger/wiremock-admin-api.yaml': { "version: ${it}" }
        ]

        def currentVersion = project.version
        def nextVersion = "${majorVersion}.${minorVersion + 1}.0"

        filesWithVersion.each { fileName, lineWithVersionTemplates ->
            def file = file(fileName)
            def lineWithVersionTemplateList = [lineWithVersionTemplates].flatten()

            lineWithVersionTemplateList.each { lineWithVersionTemplate ->
                def oldLine = lineWithVersionTemplate.call(currentVersion)
                def newLine = lineWithVersionTemplate.call(nextVersion)
                println "Replacing '${oldLine}' with '${newLine}' in ${fileName}"
                file.text = file.text.replace(oldLine, newLine)
            }
        }
    }
}

int getMajorVersion() {
    Integer.valueOf(project.version.substring(0, project.version.indexOf('.')))
}

int getMinorVersion() {
    Integer.valueOf(project.version.substring(project.version.indexOf('.') + 1, project.version.lastIndexOf('.')))
}

int getPatchVersion() {
    Integer.valueOf(project.version.substring(project.version.lastIndexOf('.') + 1))
}


task 'add-copyright-headers' {
    doLast {
        def copyrightNotice = """/*
 * Copyright (C) 2011 Thomas Akehurst
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
"""

        def srcDir = file('src')
        srcDir.eachFileRecurse { file ->
            if (file.name.endsWith(".java") && !file.text.contains(copyrightNotice)) {
                println "Adding copyright header to $file.path"
                def newFileText = copyrightNotice + file.text
                file.text = newFileText
            }
        }
    }
}

wrapper {
    gradleVersion = '7.2'
    distributionType = Wrapper.DistributionType.BIN
}
